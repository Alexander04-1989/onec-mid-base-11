#Область ПрограммныйИнтерфейс  

Процедура ДополнитьФорму(Форма) Экспорт  
	// Добавление на формы всех документов реквизита КонтактноеЛицо, 
	// а так же реквизита СогласованнаяСкидка и кнопки ПересчитатьТаблицу на форму документа ЗаказПокупателя
	ИмяФормы = Форма.ИмяФормы; 
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПравоСогласованнаяСкидка(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
    ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);	
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс 

    // Реализация добавления на формы документов ЗаказПокупателя, ПоступленияТоваровИУслуг, РеализацияТоваровИУслуг
	// реквизита КонтактноеЛицо
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) Экспорт
	ПолеВВода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВВода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВВода.ПутьКДанным = "Объект.АГ_КонтактноеЛицо";
КонецПроцедуры   

    // Реализация добавления на формы документов ОплатаОтПокупателя и ОплатаПоставщику реквизита КонтактноеЛицо 
Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) Экспорт
	ПолеВВода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	ПолеВВода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВВода.ПутьКДанным = "Объект.АГ_КонтактноеЛицо";
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПравоСогласованнаяСкидка(Форма) Экспорт  
	
	// Добавление на форму документа ЗаказПокупателя группы СкидкиПересчет
	ГруппаДатаНомер = Форма.Элементы.Добавить("СкидкаПересчет", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	ГруппаДатаНомер.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаДатаНомер.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаДатаНомер.ОтображатьЗаголовок = ЛОЖЬ; 
	ГруппаДатаНомер.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	// Добавление в созданную рпуппу реквизита СогласованнаяСкидка и установка действия ПриИзменении этого реквизита
	ПолеВВода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.СкидкаПересчет);
	ПолеВВода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВВода.ПутьКДанным = "Объект.АГ_СогласованнаяСкидка";  	
	Форма.Элементы.СогласованнаяСкидка.УстановитьДействие("ПриИзменении", "АГ_СогласованнаяСкидкаПриИзменении");
	
	// Добавление в созданную рпуппу кнопки ПересчитатьТаблицу и установка действия этой кнопки
	НоваяКоманда = Форма.Команды.Добавить("АГ_ПересчитатьТаблицу");
	НоваяКоманда.Действие = "АГ_ПересчитатьТаблицу";
	НоваяКоманда.Заголовок = "Пересчитать Таблицу";
	
	Кнопка = Форма.Элементы.Добавить("АГ_ПересчитатьТаблицу", Тип("КнопкаФормы"),Форма.Элементы.СкидкаПересчет);
	Кнопка.ИмяКоманды = "АГ_ПересчитатьТаблицу"; 
КонецПроцедуры  

#КонецОбласти

